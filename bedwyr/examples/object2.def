% A very simple object logic: a fragment of intuitionistic logic with
% universal quantifier and implication. The implication formulas are
% restricted to the ones with only atomic formulas on the left of
% implication.

% Example taken from the CADE2007 system description for Bedwyr.

Kind list,form  type.
Type nil        list.
Type cons       form -> list -> list.

Kind obj        type.
Type p          obj -> obj -> form.
Type impl       form -> form -> form.
Type $forall    (obj -> form) -> form.

Define memb : form -> list -> prop by
  memb A (cons A L);
  memb A (cons B L) := memb A L.

Define pv : list -> form -> prop by
  pv L (p X Y) := memb (p X Y) L;
  pv L (impl A B) := pv (cons A L) B;
  pv L ($forall B) := nabla x, pv L (B x).

#assert
  (forall r s t,
     pv nil ($forall x\ impl (p x r) ($forall y\ impl (p y s) (p x t))) -> r = t).

#assert_not
  (forall r s t,
     pv nil ($forall x\ impl (p x r) ($forall y\ impl (p y s) (p x t))) -> r = s).
