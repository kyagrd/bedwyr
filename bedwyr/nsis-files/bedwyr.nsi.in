##############################################################################
#  Bedwyr prover                                                             #
#  Copyright (C) 2012 Quentin Heath                                          #
#                                                                            #
#  This program is free software; you can redistribute it and/or modify      #
#  it under the terms of the GNU General Public License as published by      #
#  the Free Software Foundation; either version 2 of the License, or         #
#  (at your option) any later version.                                       #
#                                                                            #
#  This program is distributed in the hope that it will be useful,           #
#  but WITHOUT ANY WARRANTY; without even the implied warranty of            #
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the             #
#  GNU General Public License for more details.                              #
#                                                                            #
#  You should have received a copy of the GNU General Public License along   #
#  with this program; if not, write to the Free Software Foundation, Inc.,   #
#  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.               #
##############################################################################

# @configure_input@
#
# NSI input for makensis (from the Nullsoft Scriptable Install System)
# option -NOCD required

!include "MUI2.nsh"
!include "nsis-files/EnvVarUpdate.nsh"
!include "nsis-files/FileAssociation.nsh"

#
# Variables
#
########################################################################

!define PROJECT_NAME    "@PROJECT_NAME@"
!define PACKAGE_NAME    "@PACKAGE_NAME@"
!define PACKAGE_TARNAME "@PACKAGE_TARNAME@"
!define PACKAGE_VERSION "@PACKAGE_VERSION@"
!define PACKAGE_FULLNAME \
  "${PACKAGE_NAME} ${PACKAGE_VERSION}"
!define OCAML_VERSION   "@OCAML_VERSION@"
!define PACKAGE_TAGLINE "@PACKAGE_TAGLINE@"
!define PROJECT_PACKAGE "${PROJECT_NAME}\${PACKAGE_NAME}"
!define UNINSTALL_KEY \
  "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PACKAGE_NAME}"
!define SOFTWARE_KEY    "Software\${PROJECT_PACKAGE}"
!define INSTALL_DIR     "$PROGRAMFILES32\${PROJECT_PACKAGE}"
!define HKCR_EXTENSION  ".def"
!define HKCR_FILECLASS  "${PACKAGE_NAME}.Definitions.1.3"
!define HKCR_DESC       "${PACKAGE_NAME} Definitions"

#!define MUI_HEADERIMAGE
#!define MUI_HEADERIMAGE_BITMAP "150x57.bmp"
#!define MUI_HEADERIMAGE_BITMAP_NOSTRETCH
#!define MUI_HEADER_TRANSPARANT_TEXT
#!define MUI_WELCOMEFINISHPAGE_BITMAP "164x314.bmp"
#!define MUI_WELCOMEFINISHPAGE_BITMAP_NOSTRETCH
!define MUI_COMPONENTSPAGE_NODESC
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_ABORTWARNING
!define MUI_ABORTWARNING_TEXT "Nothing was installed yet. Abort now?"
!define MUI_ABORTWARNING_CANCEL_DEFAULT
!define MUI_UNABORTWARNING
!define MUI_UNABORTWARNING_TEXT "Nothing was removed yet. Abort now?"
!define MUI_UNABORTWARNING_CANCEL_DEFAULT

#!define MUI_STARTMENUPAGE_NODISABLE
Var StartMenuFolder
#!define MUI_STARTMENUPAGE_DEFAULTFOLDER ${PROJECT_PACKAGE}
#!define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKLM"
#!define MUI_STARTMENUPAGE_REGISTRY_KEY "${SOFTWARE_KEY}"
#!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "StartMenuFolder"

#
# General commands
#
########################################################################

Name "${PACKAGE_FULLNAME}"
OutFile "${PACKAGE_TARNAME}-${PACKAGE_VERSION}-installer.exe"
InstallDir "${INSTALL_DIR}"
InstallDirRegKey HKLM "${SOFTWARE_KEY}" "InstallDir"
;RequestExecutionLevel user

!insertmacro MUI_PAGE_LICENSE "nsis-files/COPYING.txt"

!insertmacro MUI_PAGE_COMPONENTS

!insertmacro MUI_PAGE_DIRECTORY

# XXX broken, doesn't take the user's choice into account
#!insertmacro MUI_PAGE_STARTMENU page_id $StartMenuFolder

!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_TEXT \
  "${PACKAGE_FULLNAME} has been installed on your computer, \
along with examples ($INSTDIR\Examples) and the manual ($INSTDIR\Doc) \
if requested."
!define MUI_FINISHPAGE_LINK "Visit the website"
!define MUI_FINISHPAGE_LINK_LOCATION "http://slimmer.gforge.inria.fr/bedwyr/"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM

!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"

InstType "Full"
InstType "Standard"
InstType "Minimal"

#
# Uninstall a previous version
#
########################################################################

Function .onInit
  SetCurInstType 1

  ReadRegStr $R0 HKLM "${UNINSTALL_KEY}" "UninstallString"
  ReadRegStr $R1 HKLM "${UNINSTALL_KEY}" "DisplayVersion"
  ReadRegStr $R2 HKLM "${SOFTWARE_KEY}" "InstallDir"
  StrCmp $R0 "" onInitdone

  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \
    "${PACKAGE_NAME} version $R1 is already installed.$\n$\n \
Click `OK` to replace it, or `Cancel` to cancel the installation of \
${PACKAGE_NAME} version ${PACKAGE_VERSION}." \
    /SD IDCANCEL IDOK onInitUninst
  Abort

;Run the uninstaller
onInitUninst:
  ClearErrors
  ExecWait '$R0 _?=$R2' ; '_?=...': do not copy the uninstaller to a temp file,
                        ; so that $R0 doesn't return as soon as its copy
                        ; starts running. Also prevents it to be uninstalled.
  IfErrors no_remove_uninstaller onInitdone
  no_remove_uninstaller:
  messageBox MB_OK|MB_ICONSTOP \
    "The previous version was not uninstalled. \
If this was not your intention, please contact support."
  Abort

onInitdone:
FunctionEnd

#
# Stuff to install
#
########################################################################

SectionGroup "!Bedwyr binaries"
  Section "x86 native code (required)"
    SectionIn 1 2 3 RO
    ${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR"
    WriteRegStr   HKLM "${SOFTWARE_KEY}" "InstallDir" "$INSTDIR"
    WriteRegStr   HKLM "${SOFTWARE_KEY}" "Version" "${PACKAGE_VERSION}"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" "DisplayName" "${PACKAGE_NAME}"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" "DisplayVersion" "${PACKAGE_VERSION}"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" "DisplayIcon" "$INSTDIR\bedwyr.exe,0"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" \
      "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
    WriteRegStr   HKLM "${UNINSTALL_KEY}" \
      "Comments" "the not-so-sound logician"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" "Publisher" "${PROJECT_NAME} project"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" \
      "QuietUninstallString" "$\"$INSTDIR\uninstall.exe$\" /S"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" "InstallLocation" "$INSTDIR"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" \
      "HelpLink" "http://slimmer.gforge.inria.fr/bedwyr/doc/quickstart.html"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" \
      "URLUpdateInfo" "http://slimmer.gforge.inria.fr/bedwyr/#download"
    WriteRegStr   HKLM "${UNINSTALL_KEY}" \
      "URLInfoAbout" "http://slimmer.gforge.inria.fr/bedwyr/"
    WriteRegDWORD HKLM "${UNINSTALL_KEY}" "NoModify" 1
    WriteRegDWORD HKLM "${UNINSTALL_KEY}" "NoRepair" 1
    ${RegisterExtension} \
      "${HKCR_EXTENSION}" "${HKCR_FILECLASS}" "${HKCR_DESC}" \
      "$INSTDIR\bedwyr.exe" "1" "${PACKAGE_NAME}"
    SetOutPath "$INSTDIR"
    File _build\src\bedwyr.exe
    WriteUninstaller "$INSTDIR\uninstall.exe"
  SectionEnd
  Section "bytecode (for OCaml ${OCAML_VERSION})"
    SectionIn 1
    SetOutPath "$INSTDIR"
    File _build\src\bedwyr.byte
  SectionEnd
SectionGroupEnd

Section "!Examples"
  SectionIn 1 2
  SetOutPath "$INSTDIR\Examples"
  File /r examples\*
SectionEnd

SectionGroup "Start Menu Shortcuts"
  Section "Programs"
    #!define MUI_STARTMENU_WRITE_BEGIN "page_id"
    StrCpy $StartMenuFolder "${PROJECT_PACKAGE}"
    SectionIn 1 2
    #!define MUI_STARTMENU_WRITE_BEGIN page_id
    SetShellVarContext all
    CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
    SetOutPath "$SMPROGRAMS\$StartMenuFolder"
    CreateShortCut "Uninstall ${PACKAGE_NAME}.lnk" \
      "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
    CreateShortCut "${PACKAGE_NAME}.lnk" \
      "$INSTDIR\bedwyr.exe" "" "$INSTDIR\bedwyr.exe" 0
    IfFileExists "$INSTDIR\bedwyr.byte" 0 +2
    CreateShortCut "${PACKAGE_NAME} - bytecode.lnk" \
      "$INSTDIR\bedwyr.byte" "" "$INSTDIR\bedwyr.exe" 0
    #!define MUI_STARTMENU_WRITE_END
  SectionEnd
  Section "Files"
    #!define MUI_STARTMENU_WRITE_BEGIN "page_id"
    StrCpy $StartMenuFolder "${PROJECT_PACKAGE}"
    SectionIn 1
    #!define MUI_STARTMENU_WRITE_BEGIN page_id
    SetShellVarContext all
    CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
    SetOutPath "$SMPROGRAMS\$StartMenuFolder"
    CreateShortCut "Doc.lnk" \
      "$INSTDIR\Doc" "" "$INSTDIR\bedwyr.exe" 1
    CreateShortCut "Examples.lnk" \
      "$INSTDIR\Examples" "" "$INSTDIR\bedwyr.exe" 1
    #!define MUI_STARTMENU_WRITE_END
  SectionEnd
SectionGroupEnd

SectionGroup "Contrib files"
  Section "Emacs files"
    SectionIn 1
    SetOutPath "$INSTDIR\Contrib"
    File /r contrib\emacs
    Rename "$INSTDIR\Contrib\emacs" "$INSTDIR\Contrib\Emacs"
  SectionEnd
  Section "Vim files"
    SectionIn 1
    SetOutPath "$INSTDIR\Contrib"
    File /r contrib\vim
    Rename "$INSTDIR\Contrib\vim" "$INSTDIR\Contrib\Vim"
  SectionEnd
SectionGroupEnd

SectionGroup "Documentation"
  SectionGroup "Quickstart guide"
    Section "HTML"
      SectionIn 1
      SetOutPath "$INSTDIR\Doc"
      File doc\quickstart.html
    SectionEnd
    Section "PDF"
      SectionIn 1 2
      SetOutPath "$INSTDIR\Doc"
      File doc\quickstart.pdf
    SectionEnd
  SectionGroupEnd
  SectionGroup "Reference manual"
    Section "HTML"
      SectionIn 1
      SetOutPath "$INSTDIR\Doc\refman"
      File doc\refman\*
    SectionEnd
    Section "PDF"
      SectionIn 1 2
      SetOutPath "$INSTDIR\Doc"
      File doc\refman.pdf
    SectionEnd
  SectionGroupEnd
  SectionGroup "Library reference"
    Section "HTML"
      SectionIn 1
      SetOutPath "$INSTDIR\Doc\libref"
      File _build\src.docdir\*
    SectionEnd
  SectionGroupEnd
SectionGroupEnd

#
# Uninstaller
#
########################################################################

Section "Uninstall"
  SetShellVarContext all

  RMDir /r "$INSTDIR\Examples"
  RMDir /r "$INSTDIR\Contrib"

  #!insertmacro MUI_STARTMENU_GETFOLDER page_id $R0
  StrCpy $StartMenuFolder "${PROJECT_PACKAGE}"
  RMDir /r "$SMPROGRAMS\$StartMenuFolder"
  RMDir "$SMPROGRAMS\${PROJECT_NAME}"

  RMDir /r "$INSTDIR\Doc"

  Delete "$INSTDIR\bedwyr.exe"
  Delete "$INSTDIR\bedwyr.byte"
  Delete "$INSTDIR\uninstall.exe"
  RMDir "$INSTDIR"
  RMDir "$PROGRAMFILES32\${PROJECT_NAME}"

  ${UnRegisterExtension} "${HKCR_EXTENSION}" "${HKCR_FILECLASS}"
  DeleteRegKey HKLM "${UNINSTALL_KEY}"
  DeleteRegKey HKLM "${SOFTWARE_KEY}"
  DeleteRegKey HKLM "Software\${PROJECT_NAME}"
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKLM" "$INSTDIR"
SectionEnd
