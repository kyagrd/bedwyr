#define "nat {x} := (x=o);(sigma y\ (x=(s y),(nat y)))".
#define "even {x} := (x=o);(sigma y\ (x=(s (s y)),(even y)))".
#define "odd {x} := (x=(s o));(sigma y\ (x=(s (s y)),(odd y)))".
#define "leq x {y} := (x=y);(sigma z\ (y=(s z),(leq x z)))".
#define "gt {x} y := (x=s y);(sigma p\ (x=(s p),(gt p y)))".

#define
  "plus {x} y z :=
    (x = o, y = z);
    (sigma m\ n\ x = (s m), z = (s n), plus m y n)".

#define "even_and_odd x y :=
  (x = o, y = s o);
  (sigma p\ x = s (s p), even_and_odd p y);
  (sigma p\ y = s (s p), even_and_odd x p)".

#define
  "mult {x} y z :=
    (x = o, z = o);
    (sigma x'\ x = s x', sigma z'\ mult x' y z', plus y z' z)".

#define
  "half {x} {h} :=
    (((x=o);(x=(s o))),(h=o));
    (sigma xx\ hh\ (x=(s (s xx)),h=(s hh), (half xx hh)))".

#define
  "double {x} {d} :=
    (x=o,d=o);
    (sigma xp\dp\ x=(s xp), d=(s(s dp)), double xp dp)".

#define
  "ack m n a :=
     (m = o, a = s n);
     (sigma p\ n = o, m = s p, ack p (s o) a);
     (sigma p\q\b\ m = s p, n = s q, ack m q b, ack p b a)".
