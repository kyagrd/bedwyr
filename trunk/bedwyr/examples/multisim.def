% Check inclusions of finite state automata
% using the multi-simulation characterization.
%
% We use Bedwyr's tabling mechanism which actually builds
% a somehow minimal multi-simulation establishing the query.

Kind    state,
        label   type.

Type    s,z     label.
Type    accept,
        $nat,
        even,
        odd,
        half,
        half_s,
        is_z,
        loop    state.

% lt : state -> state -> o.
% The order is as folllows: accept < $nat < even < odd < ...
% We could have hardcoded the transivite closure for maximum efficiency,
% but Bedwyr can handle it using loop detections.
% However, we have to distinguish lt and lt0 so that loops are not too wild.

Define lt0 : state -> state -> prop by
  lt0 accept $nat;
  lt0 $nat   even;
  lt0 even   odd;
  lt0 odd    half;
  lt0 half   half_s;
  lt0 half_s is_z;
  lt0 is_z   loop.
Define lt : state -> state -> prop by
  lt X Y := lt0 X Y \/ lt0 X Z /\ lt Z Y.

% Union of sorted lists, removes redundancies.

Define union : list state -> list state -> list state -> prop by
  union X nil X;
  union nil X X;
  union (X :: L) (X :: L') L'' := union L (X :: L') L'';
  union (X :: L) (Y :: L') (X :: L'') := lt X Y /\ union L (Y :: L') L'';
  union (X :: L) (Y :: L') (Y :: L'') := lt Y X /\ union (X :: L) L' L''.

% Declare accepting states.

Define accepts : state -> prop by
  accepts accept.

% Given a non-deterministic transition relation p-a->p',
% we would be able to handle checking that a list contains all transitions
% p-a->p', but not to proof-search it in any reasonable way.
% Instead, we give the transitions in a determinized way.
%
% The lists should be sorted, the relation should be functional AND TOTAL.

Define steps : state -> label -> list state -> prop by
  steps even z (accept :: nil);
  steps even s (odd :: nil);
  steps odd  s (even :: nil);
  steps odd  z nil;
  steps $nat z (accept :: nil);
  steps $nat s ($nat :: nil);
  steps accept X nil;
  steps half   z (accept :: nil);
  steps half   s (half_s :: is_z :: nil);
  steps is_z   z (accept :: nil);
  steps is_z   s nil;
  steps half_s z nil;
  steps half_s s (half :: nil);
  steps loop   s (loop :: nil);
  steps loop   z (loop :: nil).

% Non-deterministic version of steps.

Define step : state -> label -> state -> prop by
  step P A P' := steps P A All /\ member P' All.

% Extension of acceptance and transitions to sets of states.

Define multi_steps : list state -> label -> list state -> prop by
  multi_steps nil A nil;
  multi_steps (Q :: S) A L'' :=
    steps Q A L /\ multi_steps S A L' /\ union L L' L''.

Define subset : list state -> list state -> prop by
  subset nil nil;
  subset L (A :: L') := subset L L';
  subset (A :: L) (A :: L') := subset L L'.

Define multi_step : list state -> label -> list state -> prop by
  multi_step X A Y := multi_steps X A X' /\ subset Y X'.

Define multi_accepts : list state -> prop by
  multi_accepts Q := member S Q /\ accepts S.

% The definition of multisim is with a subset of Q' (the maximal multi_steps
% result) but it is sound to take all of them.

Define coinductive multisim : state -> list state -> prop by
  multisim P Q :=
    (forall A P', step P A P' -> exists Q', multi_steps Q A Q' /\ multisim P' Q') /\
    (accepts P -> multi_accepts Q).

% Some extra checks that would work but pollute the table.
#assert (forall x, multi_steps (even :: odd :: nil) z x ->
               x = (accept :: nil)).
#assert (forall x, multi_steps (even :: odd :: nil) s x ->
               x = (even :: odd :: nil)).
#assert (forall x, multisim accept (accept :: x)).
#assert (multisim $nat ($nat :: nil)).

#clear_table multisim.
#assert (multisim $nat (even :: odd :: nil)).
#show_table multisim.

#clear_table multisim.
#assert (multisim $nat (half :: nil)).
#show_table multisim.

#clear_table multisim.
#assert (multisim $nat ($nat :: loop :: nil)).
#show_table multisim.
